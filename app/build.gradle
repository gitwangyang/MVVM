apply plugin: 'com.android.application'

//当前beta版本号
ext.build_number = "beta1";
//显示的时间格式，形如20170710
def releaseTime(){
    return new Date().format("yyyyMMdd",TimeZone.getDefault());
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId "com.dotawang.mvvm"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        ndk {
            abiFilters 'armeabi', 'x86'
        }
        //语言类型
        resConfigs "en", "fr"
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['jniLibs']
        }
    }

    signingConfigs {
        release {
            storeFile file(RELEASE_STOREFILE)
            storePassword RELEASE_STOREPASSWORD
            keyAlias RELEASE_KEYALIAS
            keyPassword RELEASE_KEYPASSWORD
        }
        debug {
            storeFile file(DEBUG_STOREFILE)
            storePassword DEBUG_STOREPASSWORD
            keyAlias DEBUG_KEYALIAS
            keyPassword DEBUB_KEYPASSWORD
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    configurations.all {
        resolutionStrategy.force 'com.android.support:support-annotations:26.1.0'
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude'META-INF/rxjava.properties'
    }


    // 打包命名
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                // 显示app名称+当前版本号
                def fileName = "汪洋-V${variant.versionName}"
                // 如果有渠道，显示渠道名""
                if (variant.flavorName!=""){
                    fileName = fileName + "-${variant.flavorName}"
                }
                // 加入打包时间
                fileName = fileName + "-${releaseTime()}"
                // 如果是debug版本，加入beta版本号
                if ('debug'.equals(variant.buildType.name)) {
                    fileName = fileName + "-${build_number}.apk"
                } else {
                    // 如果是release版本，直接显示
                    fileName = fileName + "-release.apk"
                }
                outputFileName = fileName
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation "com.android.support:support-annotations:$rootProject.supportLibraryVersion"
    implementation "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
    implementation "com.android.support:design:$rootProject.supportLibraryVersion"
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.github.bumptech.glide:glide:4.6.1'
    implementation 'com.jakewharton:butterknife:8.8.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'
    compile 'org.greenrobot:eventbus:3.0.0'
}